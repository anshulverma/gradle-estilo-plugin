group = 'net.anshulverma.gradle'
version = '0.0.1-SNAPSHOT'

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
    classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
  }
}

apply plugin: 'groovy'
apply plugin: 'idea'

compileGroovy {
  sourceCompatibility = '1.6'
  targetCompatibility = '1.6'
}

def javaApiUrl = 'http://docs.oracle.com/javase/1.7.0/docs/api/'
def groovyApiUrl = 'http://groovy.codehaus.org/gapi/'
tasks.withType(Javadoc) {
  options.links(javaApiUrl, groovyApiUrl)
}

repositories {
  mavenCentral()
}

task wrapper(type: Wrapper, description: 'Gradle Wrapper task') {
  gradleVersion = '2.4'
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile dependencies.create('com.puppycrawl.tools:checkstyle:6.7') {
    transitive = false
  }
  compile 'commons-io:commons-io:2.4'
  compile 'javax.xml:jaxb-api:2.1'
  compile 'javax.xml:jaxb-impl:2.1'

  compile group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.7'
  testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
    exclude(module: 'groovy-all')
  }
}

ext {
  coverageLimits = [
      'instruction': 84,
      'branch'     : 44,
      'line'       : 93,
      'complexity' : 69,
      'method'     : 79,
      'class'      : 100
  ]
}

apply from: "$rootDir/gradle/code_quality.gradle"
apply from: "$rootDir/gradle/reports.gradle"
apply from: "$rootDir/gradle/publish.gradle"
